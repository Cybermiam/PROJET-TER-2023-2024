PREFIX patrimalpr: <http://patrimalp.org/resource/>
PREFIX patrimalpo: <http://patrimalp.org/ontologies/patrimalp#>
PREFIX patrimalpgraph:  <http://patrimalp.org/graph/>
PREFIX brocartsr:  <http://patrimalp.org/brocarts/resource/> 
PREFIX brocartso: <http://patrimalp.org/brocarts/onto#>
PREFIX brocartsvoc: <http://patrimalp.org/brocarts/thesaurus#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
	?siteUri brocartso:oeuvre ?oeuvreUri.
	
	# Following links domain oeuvreUri resource of type Sculpture to all its ranges
	?oeuvreUri a patrimalpo:Scuplture;
            rdfs:label ?titre;
			# identification
			brocartso:creationDate ?lit_dateCreation;	   
			brocartso:inventoryNumber ?inventaire;		
            brocartso:protectionLevel ?niveauProtectionUri;
			brocartso:siteOfConservation ?siteActuelUri;
			brocartso:siteOfOrigin ?siteProvenanceUri;	
			brocartso:period ?period;
			# general informations
			brocartso:denomination ?denominationUri;
			brocartso:material ?materiauUri;
			brocartso:dimensions ?lit_dimensions;
			# on ne génère pas pour le moment le détail des dimensions
			# brocartso:width ?lit_hauteur;
			# brocartso:height ?lit_largeur;
			# brocartso:depth ?lit_profondeur;
			brocartso:iconography ?iconographie;
			brocartso:missingElements ?elementsManquants;	
			#images	
			patrimalpo:images ?imageOeuvreUri.

	?imageOeuvreUri a patrimalpo:Image;
		patrimalpo:auteur ?lit_auteur;
		patrimalpo:description ?lit_description;
		patrimalpo:imageUrl ?imageUri.
} 

FROM <file:../inputData/oeuvres.tsv> 
WHERE {
 BIND (URI(CONCAT(tarql:expandPrefix("brocartsr"), 'S', ENCODE_FOR_URI(?oeuvreID))) AS ?oeuvreUri)
 BIND(URI(CONCAT(tarql:expandPrefix("brocartsr"), 'S', ENCODE_FOR_URI(?oeuvreID), '_image_', ENCODE_FOR_URI(?imageID))) AS ?imageOeuvreUri)	
 BIND (URI(CONCAT(tarql:expandPrefix("patrimalpr"), 'site', ENCODE_FOR_URI(?siteConservationID))) AS ?siteUri)	
 BIND (URI(CONCAT(tarql:expandPrefix("patrimalpr"), 'site', ENCODE_FOR_URI(?siteProvenanceID))) AS ?siteProvenanceUri) 
 BIND (URI(CONCAT(tarql:expandPrefix("brocartsvoc") , "denomination", ENCODE_FOR_URI(?denomination))) AS ?denominationUri)
 BIND (URI(CONCAT(tarql:expandPrefix("brocartsvoc"), "material", ENCODE_FOR_URI(?materiau))) AS ?materiauUri)
 BIND (URI(CONCAT(tarql:expandPrefix("brocartsvoc"), "protectionLevel", ENCODE_FOR_URI(?niveauProtection))) AS ?niveauProtectionUri)
 BIND (xsd:float(?hauteur) AS ?lit_hauteur)
 BIND (xsd:float(?largeur) AS ?lit_largeur)
 BIND (xsd:float(?profondeur) AS ?lit_profondeur)
 BIND (CONCAT('(L x H x P) :', ?largeur, ' x ', ?hauteur, ' x ', ?profondeur, ' ' , ?dimensionsUnit) AS ?lit_dimensions)
 BIND (xsd:string(?auteur) AS ?lit_auteur)
 BIND (xsd:string(?description) AS ?lit_description)
 BIND (xsd:string(?inventaire) AS ?lit_inventaire)	
 BIND (STRDT(?dateCreation, xsd:date) AS ?lit_dateCreation)
 BIND (xsd:string(?period) AS ?lit_period)
 BIND (xsd:string(?elementsManquants) AS ?lit_elementsManquants)
 BIND (CONCAT('http://lig-tdcge.imag.fr/steamer/patrimalp/public/resources/patrimalp/brocarts/oeuvres/', ?image) AS ?imageUri)
}